buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.3.RELEASE'
        classpath 'org.openapitools:openapi-generator-gradle-plugin:4.3.1'
    }
}

plugins {
    id 'org.openjfx.javafxplugin' version '0.0.9'
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.swing', 'javafx.media', 'javafx.web']
    version = "11.0.2" //12-ea+8"
}


repositories {
    mavenCentral()
}
apply {
    plugin 'java-library'
    plugin 'org.openapi.generator'
    plugin 'application'
}

ext.lombokVersion = '1.18.6'

sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14


dependencies {

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"


    compile 'io.gsonfire:gson-fire:1.8.0'
    compile 'com.squareup.okhttp3:okhttp:4.9.0'
    compile 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    compile 'com.google.code.gson:gson:2.8.5'
    compile 'javax.annotation:javax.annotation-api:1.3.2'
    compile 'com.google.code.findbugs:jsr305:3.0.2' //Nullable
    compile 'org.controlsfx:controlsfx:11.0.0'

    compile 'org.slf4j:slf4j-api:1.7.25'

    runtime 'ch.qos.logback:logback-core:1.2.3'
    runtime 'ch.qos.logback:logback-classic:1.2.3'

    compile 'io.swagger:swagger-annotations:1.5.22'


    compile 'commons-io:commons-io:2.7'


    testCompile('org.junit.jupiter:junit-jupiter-engine:5.2.0')
}



def dir = project.file("../../api/".toString())
def openApiInputs = []
dir.eachFileRecurse(groovy.io.FileType.FILES) { file ->
    if (file.getName().endsWith(".yaml"))
        openApiInputs << file
}



// Iterate on all swagger files and generate a task for each one with the nomenclature openApiGenerate + swagger name
openApiInputs.each {
    def apiName = it.getName().replace(".yaml", "");

    def openApiGenerateTask = tasks.create("openApiGenerate" + apiName.capitalize(), org.openapitools.generator.gradle.plugin.tasks.GenerateTask.class, {
        //verbose = true
        generatorName = "java"
        inputSpec = file("../../api/" + apiName + ".yaml").toString()
        outputDir = "$buildDir/generated/openapi/".toString()
        apiPackage = "de.noventi.cm.client.java." + apiName + ".api"
        modelPackage = "de.noventi.cm.client.java." + apiName + ".model"
        configOptions = [
                dateLibrary: "java8"
        ]
    })

    openApiGenerateTask.outputs.upToDateWhen { false }
    openApiGenerateTask.outputs.cacheIf { false }
    tasks.compileJava.dependsOn openApiGenerateTask
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/openapi/src/main/java")
        }
    }
}

application {
    mainClass = 'de.noventi.cm.client.java.Application'
}

test {
    useJUnitPlatform()
}