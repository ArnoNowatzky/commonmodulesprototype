plugins {
    id 'com.google.cloud.tools.jib' version '2.6.0'
}

apply {
    plugin 'java-library'
    plugin 'org.openapi.generator'
    plugin 'org.springframework.boot'
    plugin 'io.spring.dependency-management'
    plugin 'vsa.releasemaven'
}

println "Applying vsa.releasemaven"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

ext.lombokVersion = '1.18.6'

dependencies {

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1' //TODO unused import, check how to get rid of

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-activemq'
    //TODO compile 'org.springframework.boot:spring-boot-starter-security'

    compile 'io.swagger:swagger-annotations:1.5.22'
    compile 'javax.validation:validation-api:2.0.1.Final'

    compile 'de.noventi.cm:commonModuleBase:0.1-SNAPSHOT'

    //TODO discuss compile 'org.springframework.boot:spring-boot-starter-actuator'


    runtime 'ch.qos.logback:logback-core:1.2.3'
    runtime 'ch.qos.logback:logback-classic:1.2.3'
    runtime 'org.postgresql:postgresql:42.2.18'

    testCompile('org.springframework.boot:spring-boot-starter-test:2.4.0')
    testCompile('org.junit.jupiter:junit-jupiter-engine:5.2.0')
}

springBoot {
    mainClassName = 'de.noventi.cm.service.ServiceApplication'
}

openApiGenerate {
    generatorName = "spring"
    library = "spring-boot"
    inputSpec = file('../api/src/main/resources/api/service/service.yaml').toString()
    outputDir = "$buildDir/generated/openapi".toString()
    apiPackage = "de.noventi.cm.service.api"
    modelPackage = "de.noventi.cm.service.model"
    configOptions = [
            dateLibrary: "java8-localdatetime",
            java8: "true",
            useBeanValidation: "true",
            interfaceOnly: "true",
            useOptional: "true",
            useLombok: "true"
    ]
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/openapi/src/main/java")
        }
    }
}

tasks.compileJava.dependsOn tasks.openApiGenerate

println "Docker user = " + project.properties['awinta.dockerUser']
println "Docker pwd = " + project.properties['awinta.dockerPassword']

jib {
    to {
        image = project.provider {"awinta-docker.intra.vsa.de/noventi.cm/commonmoduleservice:" + version}
        auth {
            username = project.properties['awinta.dockerUser']
            password = project.properties['awinta.dockerPassword']
        }

    }
    from {
        image = 'docker.intra.vsa.de/amazoncorretto:11.0.9-alpine'
        auth {
            username = project.properties['awinta.dockerUser']
            password = project.properties['awinta.dockerPassword']
        }
    }
    container {
        mainClass = 'de.noventi.cm.service.ServiceApplication'
        environment = ['SPRING_DATASOURCE_URL':'jdbc:postgresql://postgresql:5432/service',
                       'ACTIVE-MQ_BROKER-URL':'tcp://activemq:61616']
        ports = ['8002/tcp']
    }
}



//TODO https://stackoverflow.com/questions/61197984/bootjar-mavenjar-artifact-wasnt-produced-by-this-build
configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf {
            it.buildDependencies.getDependencies(null).contains(jar)
        }
        it.outgoing.artifact(bootJar)
    }
}

test {
    useJUnitPlatform()
}

tasks.build.dependsOn tasks.releaseLocalSnapshot
tasks.build.dependsOn tasks.jib
