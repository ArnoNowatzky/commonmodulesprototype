buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url "https://repo.intra.vsa.de/artifactory/maven/" }
    }

    dependencies {
        classpath 'de.vsa.gradle:vsareleasemaven:6.101'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.3.RELEASE'
        classpath 'org.openapitools:openapi-generator-gradle-plugin:4.3.1'
    }
}

plugins {
    id 'com.bmuschko.docker-spring-boot-application' version '6.6.1'
}

repositories {
    mavenCentral()
}

group = 'de.noventi.cm'
version = 0.1

apply {
    plugin 'java-library'
    plugin 'org.openapi.generator'
    plugin 'org.springframework.boot'
    plugin 'io.spring.dependency-management'
    plugin 'vsa.releasemaven'
}

ext.lombokVersion = '1.18.6'
ext.jaxbVersion = '2.3.0'

dependencies {

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation 'org.openapitools:jackson-databind-nullable:0.2.1' //TODO unused import, check how to get rid of

    compile 'org.springframework.boot:spring-boot-starter-web'
    //TODO compile 'org.springframework.boot:spring-boot-starter-security'

    compile 'io.swagger:swagger-annotations:1.5.22'
    compile 'commons-io:commons-io:2.7'
    compile 'org.apache.commons:commons-lang3:3.11'


    runtime 'ch.qos.logback:logback-core:1.2.3'
    runtime 'ch.qos.logback:logback-classic:1.2.3'
    runtime 'org.postgresql:postgresql:42.2.18'



    //JAXB
    compile 'com.sun.activation:javax.activation:1.2.0'
    compile "javax.xml.bind:jaxb-api:${jaxbVersion}"
    compile "com.sun.xml.bind:jaxb-core:${jaxbVersion}"
    compile "com.sun.xml.bind:jaxb-impl:${jaxbVersion}"

    testCompile('org.springframework.boot:spring-boot-starter-test:2.4.0')
    testCompile('org.junit.jupiter:junit-jupiter-engine:5.2.0')
}

springBoot {
    mainClassName = 'de.noventi.cm.runtime.RuntimeApplication'
}

openApiGenerate {
    generatorName = "spring"
    library = "spring-boot"
    inputSpec = file('../api/runtime.yaml').toString()
    outputDir = "$buildDir/generated/openapi".toString()
    apiPackage = "de.noventi.cm.runtime.api"
    modelPackage = "de.noventi.cm.runtime.model"
    configOptions = [
            dateLibrary: "java8-localdatetime",
            java8: "true",
            useBeanValidation: "true",
            interfaceOnly: "true",
            useOptional: "true",
            useLombok: "true"
    ]
}

sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/openapi/src/main/java")
        }
    }
}

tasks.compileJava.dependsOn tasks.openApiGenerate

println "Docker user = " + project.properties['awinta_dockerUser']

docker {
    registryCredentials {
        url = 'https://vsa-docker.intra.vsa.de/'
        username = project.properties['awinta_dockerUser']
        password = project.properties['awinta_dockerPassword']
    }
    springBootApplication {
        baseImage = 'openjdk:14'
        ports = [8080]
        images = ["vsa-docker.intra.vsa.de/noventi.cm/runtime:${jar.archiveVersion.get()}"]
    }
}

tasks.dockerBuildImage.dependsOn classes, bootJar

//TODO https://stackoverflow.com/questions/61197984/bootjar-mavenjar-artifact-wasnt-produced-by-this-build
configurations {
    [apiElements, runtimeElements].each {
        it.outgoing.artifacts.removeIf {
            it.buildDependencies.getDependencies(null).contains(jar)
        }
        it.outgoing.artifact(bootJar)
    }
}

task copyDependencies(type: Copy) {
    destinationDir libsDir
    from configurations.runtime
}

task javapackager(type: Exec, dependsOn: [assemble, copyDependencies]) {
    def nativeType
    if (System.properties['os.name'].toLowerCase().contains('windows'))
        nativeType = 'msi'
    if (System.properties['os.name'].toLowerCase().contains('mac'))
        nativeType = 'dmg'
    if (System.properties['os.name'].toLowerCase().contains('linux'))
        nativeType = 'rpm'

    def dependencies = []
    configurations.runtime.forEach({ file ->
        dependencies.add('-srcfiles')
        dependencies.add(file.getName())
    })

    workingDir project.projectDir
    commandLine = [
            'javapackager',
            '-deploy',
            '-nosign',
            '-native', nativeType,
            '-outdir', "${buildDir}/distribution",
            '-outfile', project.name,
            '-name', 'Application Seed',
            '-appclass', springBoot.mainClassName,
            '-srcdir', libsDir,
            '-srcfiles', jar.archiveName
    ] + dependencies
}
